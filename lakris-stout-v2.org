#+TITLE: Lakris stout v2
#+AUTHOR: Vegard Sjonfjell og Johan Fjeldtvedt
#+DATE: Lørdag 4. April 2019
#+OPTIONS: toc:nil

* Meta
Basert på https://homebrewacademy.com/oatmeal-stout-recipe/

* Tidspunkt
Brygging 2019-04-??

* Ingredienser
#+BEGIN_SRC hy :results none :session bjarne :exports none
     (setv volume 28)
     (setv orig-volume 21.76612)
     (setv boil-time 90)

     (defmacro define-ingredients [coll-name &rest args]
       `(do
          (global ~coll-name)
          (setv ~coll-name ~args)))

     (deftag scale [orig-value] `(* ~orig-value (/ volume orig-volume)))
     (deftag lbs [value] `(* ~value 453.592))
     (deftag oz [value] `(* ~value 28.3495))

     (defn format-time [_ item]
       (.format "{0} min" (:time item)))

     (defn format-grams [_ item]
       (.format "{:.0f} g" (:grams item)))

     (defn get-total [coll key]
       (reduce + (map (fn [item] (get item key)) coll)))

     (defn --percentage [key]
       (fn [coll item]
         (setv total (get-total coll key))
         (.format "{:.2f} %" (* 100 (/ (get item key) total)))))

     (defmacro percentage [key]
       `(--percentage '~key))

     (defn to-table [coll keys]
       (list (map
               (fn [item]
                 (list (map (fn [k] (if (keyword? k)
                                        (get item k)
                                        (k coll item)))
                            keys)))
               coll)))
#+END_SRC
#+BEGIN_SRC hy :results none :session bjarne :exports none
     (define-ingredients grains
       {:grams #scale #lbs 7    :name "Maris Otter"}
       {:grams #scale #lbs 3    :name "Munich (9 SRM)"}
       {:grams #scale #lbs 1.75 :name "Havregryn"}
       {:grams #scale #lbs 1    :name "Crystal 80L"}
       {:grams #scale #lbs 0.75 :name "Sjokolademalt"}
       {:grams #scale #lbs 0.5  :name "Røstet bygg (umaltet?)"})

     (define-ingredients hops
       {:time boil-time :name "Williamette" :grams #scale #oz 1.5}
       {:time 0         :name "Lakrisrot"   :grams 200 :desc "Lag en te av lakrisroten"})
#+END_SRC

** Malt
#+BEGIN_SRC hy :session bjarne :results output table :exports results
      (to-table grains [:name format-grams (percentage :grams)])
#+END_SRC

#+RESULTS:
| Maris Otter            | 4085 g | 50.00 % |
| Munich (9 SRM)         | 1751 g | 21.43 % |
| Havregryn              | 1021 g | 12.50 % |
| Crystal 80L            | 584 g  | 7.14 %  |
| Sjokolademalt          | 438 g  | 5.36 %  |
| Røstet bygg (umaltet?) | 292 g  | 3.57 %  |

** Humle
#+BEGIN_SRC hy :session bjarne :results output table :exports results
      (to-table hops [:name format-grams (percentage :grams)])
#+END_SRC
    
#+RESULTS:
| Williamette | 55 g  | 21.48 % |
| Lakrisrot   | 200 g | 78.52 % |
    
* Mesking
Mesk i 60 minutter ved gjennomsnittlig 64 grader. Bruk 22.3 liter meskevann

* Skylling
Varm opp 18 liter skyllevann. Tøm etter behov.
   
* Koking
Kok i 90 minutter.
Humle disponeres i kokeapparat på følgende måte.
   
#+BEGIN_SRC hy :session bjarne :results output table :exports results
      (to-table hops [format-time format-grams])
#+END_SRC
   
#+RESULTS:
| 90 min | 55 g  |
| 0 min  | 200 g |
   
* Gjæring
   
* Tapping
